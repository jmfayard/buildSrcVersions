
pluginManagement {
    repositories {
        mavenLocal()
        gradlePluginPortal()
    }

    final File versionFile = new File(rootDir.parentFile, "plugins/version.txt")
    final String pluginsVersion = versionFile.readLines().first()
    plugins {
        id("de.fayard.refreshVersions").version(pluginsVersion)
    }
}

plugins {
    id("com.gradle.enterprise").version("3.1.1")
    id 'de.fayard.refreshVersions'
}

gradleEnterprise {
    buildScan {
        termsOfServiceUrl = "https://gradle.com/terms-of-service"
        termsOfServiceAgree = "yes"
    }
}

rootProject.name = 'sample-groovy'

refreshVersions {
    featureFlags {
        enable 'LIBS'
        disable 'GRADLE_UPDATES'
    }


    rejectVersionIf {
        versionKey != "version.kotlinx.coroutines" && candidate.stabilityLevel.isLessStableThan(current.stabilityLevel)
    }

}
